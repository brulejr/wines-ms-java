plugins {
	id 'org.springframework.boot' version '2.5.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "com.google.cloud.tools.jib" version "3.1.2"
	id 'java'
	id 'jacoco'
}

group = 'io.jrb.labs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('mapstructVersion', '1.4.2.Final')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.javatuples:javatuples:1.2'

	compileOnly 'org.projectlombok:lombok'

	implementation "org.mapstruct:mapstruct:${mapstructVersion}"

	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
	testImplementation 'io.projectreactor:reactor-test'

	testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
}

test {
	useJUnitPlatform()
}

jib {
	to {
		image "brulejr/wines-ms-java"
		auth {
			// retrieve from Jenkins
			username "${System.env.DOCKERHUB_CREDENTIALS_USR}"
			password "${System.env.DOCKERHUB_CREDENTIALS_PSW}"
		}
	}
}
